plugins {
    id "com.google.protobuf"
    id 'com.android.application'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
    id 'img-optimizer'
}

android {
    compileSdk 34
    namespace "ly.pp.justpiano3"

    kotlinOptions {
        jvmTarget = '17'
    }

    defaultConfig {
        applicationId "ly.pp.justpiano3"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 53
        versionName "4.9"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
                abiFilters 'arm64-v8a', 'x86_64'
                arguments "-DANDROID_PLATFORM=android-29", "-DANDROID_STL=c++_shared"
            }
        }
        buildConfigField("String", "BUILD_TIME", "\"" + new Date().format('yyyyMMddHHmmss') + "\"")
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties']
        }
    }

    signingConfigs {
        debug {
            storeFile file('../key.jks')
            storePassword 'as2134.@u'
            keyAlias 'as2134u'
            keyPassword 'as2134.@u'
        }

        release {
            storeFile file('../key.jks')
            storePassword 'as2134.@u'
            keyAlias 'as2134u'
            keyPassword 'as2134.@u'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            java {
                srcDirs += ['src/main/java', 'build/generated/source/proto']
            }
            proto {
                srcDir 'src/main/protobuf'
                include '**/*.proto'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
            debuggable true
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

optimizerOptions {
    triggerSize 5
    type "lossy"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    // jlayer，用于解码mp3音源
    implementation 'javazoom:jlayer:1.0.1'
    // netty
    implementation 'io.netty:netty-all:4.1.104.Final'
    // protobuf，版本号最好和服务端保持一致，降低序列化问题风险
    implementation 'com.google.protobuf:protobuf-java:3.23.0'
    // okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.21"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0'
    // sqlite、Room库
    implementation 'androidx.sqlite:sqlite-ktx:2.4.0'
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.paging:paging-runtime-ktx:3.2.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    kapt 'androidx.room:room-compiler:2.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'
    // native-crash 用于上报C++异常闪退日志
    implementation 'io.github.BAByte:native-crash:1.0.6@aar'
    implementation 'net.margaritov.preference.colorpicker.ColorPickerPreference:ColorPickerPreference:1.0.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.23.0'
    }

    generateProtoTasks {
        all().each { task ->
            task.outputs.upToDateWhen { false } // always run the task
            task.builtins {
                java {
                    outputSubDir = '../' // set the output directory to the parent directory
                }
            }
        }
    }
}